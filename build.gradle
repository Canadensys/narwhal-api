apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'tomcat'

repositories {
	maven {url 'http://repo.opengeo.org'}
	maven {url 'http://download.java.net/maven/2'}
	maven {url 'http://repo.opengeo.org'}

    mavenCentral()
    mavenLocal()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:0.9.7'
    }
}

ext {
    springVersion = '3.2.1.RELEASE'
    jacksonVersion = '2.1.4'
	geotoolsVersion = '10-SNAPSHOT'
	sourceEncoding = 'UTF-8'
	tomcatVersion = '6.0.29'
}

compileJava.options.encoding = sourceEncoding
compileTestJava.options.encoding = sourceEncoding

dependencies {
	providedCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
	providedCompile group: 'javax.servlet', name: 'jsp-api', version: '2.0'
	
	compile group: 'org.geotools', name: 'gt-geojson', version: geotoolsVersion
	compile group: 'org.geotools', name: 'gt-xml', version: geotoolsVersion
	compile group: 'net.canadensys', name: 'narwhal-processor', version: '1.0'
	compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
	compile group: 'org.springframework', name: 'spring-web', version: springVersion
	compile group: 'org.springframework', name: 'spring-oxm', version: springVersion
	compile group: 'org.springframework', name: 'spring-context-support', version: springVersion
	
	compile group: 'org.hibernate', name: 'hibernate-validator', version: '4.3.1.Final'
	
	compile group: 'log4j', name: 'log4j', version: '1.2.17'
	compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.2'
	
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
	
	compile group: 'org.freemarker', name: 'freemarker', version: '2.3.19'
	
	testCompile group: 'junit', name: 'junit', version: '4.8.2'
	testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'	
    testCompile group: 'org.springframework', name: 'spring-test', version: springVersion
    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '0.8.1'
    
    tomcat("org.apache.tomcat:catalina:${tomcatVersion}")
    tomcat("org.apache.tomcat:coyote:${tomcatVersion}")
    tomcat("org.apache.tomcat:jasper:${tomcatVersion}")
}

//WAR plugin settings
war {
	webInf { from "../canadensys-view-includes/WEB-INF" }
	webInf { from "config" }
}

test {
    systemProperties 'file.encoding': 'UTF-8'
}

tomcatRunWar {
    httpPort = 9966
    contextPath = 'tools'
}

task concatProperties << {
	ant.concat(
		append : "yes",
		destfile:"${sourceSets.main.output.resourcesDir}/messages_en.properties") {
			fileset(dir:'../canadensys-view-includes/resources/',includes:'messages_en.properties')
	}
	
	ant.concat(
		append : "yes",
		destfile:"${sourceSets.main.output.resourcesDir}/messages_fr.properties") {
			fileset(dir:'../canadensys-view-includes/resources/',includes:'messages_fr.properties')
	}
}
//Do concatProperties before WAR
war.dependsOn concatProperties
	